@ require anonymous_resources:hash
@ require inventory_tweaks:sort/id
@ require inventory_tweaks:sort/tree
@ require anonymous_resources:storage

# We use entity sort for this
# The numbers are stored at y pos of entities
# Then the entities are targeted with sort=nearest
# Armor stands are used because we need them for the predicate anyway

var self_tag = get_unique('self')
var summon_tag = get_unique('summon')
var unique_tag = get_unique('unique')

# Summon the armor stands in mass
in overworld summon minecraft:armor_stand ${load_coords(0.5, 0, 0.5)} {
    Marker: 1b,
    Invisible: 1b,
    Tags: [$unique_tag],
    Passengers: ${new nbt([nbt<{id:'minecraft:armor_stand', Tags:[$summon_tag]}>] * 27)}
}
# Kill the root armor stand
kill @e[type=minecraft:armor_stand,tag=$unique_tag]

var slot = anonymous_score()
set $slot = 0
# Operate on the passenger armor stands
as @e[type=minecraft:armor_stand,tag=$summon_tag] function /sort_armor_stands {
    eval generateFunctionTree(
        resource</tree>,
        int_range<0..27>,
        4,
        function (range, next) {
            if score deref $slot matches $range function $next
        },
        function (i) {
            # Copy the item to the armor stand's mainhand
            if score deref $slot matches $i
                item replace entity @s weapon.mainhand from block ~ ~ ~ ${'container.' + i}
        }
    )
    # Copy the corresponding item to the armor stand's mainhand
    function /tree
    # Get the item id
    function inventory_tweaks:sort/id
    # If the armor stand doesn't hold anything, kill it
    if score deref $id matches 0 kill @s
    # If the id is not 0, copy it to the armor stand's Pos[1], scale down
    unless score deref $id matches 0
        set @s.Pos[1] * ${Math.pow(2, -31)} (double) = $id
    set $slot += 1
}
# Place items from the armor stands to a shulker box based on their Pos[1]
in overworld set ${shulker_box(0)}.Items = []
set $slot_id = 0

in overworld as @e[type=minecraft:armor_stand,tag=$summon_tag,x=$load_x,y=-2,z=$load_z,dy=4,sort=nearest] function /../insert {
    function inventory_tweaks:sort/tree
    kill @s
    set $slot_id += 1
}

# Loot the shulker box to another shulker box to merge items
in overworld set ${shulker_box(1)}.Items = []
in overworld loot insert ${shulker_box(1)} mine ${shulker_box(0)} air{drop_contents:1b}

# Copy the final shulker box to a storage
var storage = anonymous_storage()
in overworld set $storage = ${shulker_box(1)}.Items
set (~ ~ ~).Items = $storage
