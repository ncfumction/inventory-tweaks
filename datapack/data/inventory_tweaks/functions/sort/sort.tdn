@ require anonymous_resources:hash
@ require inventory_tweaks:sort/id
@ require inventory_tweaks:sort/tree

# We use entity sort for this
# The numbers are stored at y pos of entities
# Then the entities are targeted with sort=nearest
# Armor stands are used because we need them for the predicate anyway

var self_tag = get_unique('self')
var summon_tag = get_unique('summon')
var unique_tag = get_unique('unique')

# I'm not using using because that'll result in more indentation
tag @s add $self_tag

# This whole thing happens in overworld
in overworld function /in_overworld {
    # Summon the armor stands in mass
    summon minecraft:armor_stand ${load_coords(0, 0, 0)} {
        Marker: 1b,
        Invisible: 1b,
        Tags: [$unique_tag],
        Passengers: ${new nbt([nbt<{id:'minecraft:armor_stand', Tags:[$summon_tag]}>] * 27)}
    }
    # Kill the root armor stand
    kill @e[type=minecraft:armor_stand,tag=$unique_tag]
    # Operate on the passenger armor stands
    var slot = anonymous_score()
    set $slot = 0
    as @e[type=minecraft:armor_stand,tag=$summon_tag] function /../sort_armor_stands {
        # Change the armor stands' Pos based on the items
        eval generateFunctionTree(
            resource</tree>,
            int_range<0..27>,
            4,
            function (range, next) {
                if score deref $slot matches $range function $next
            },
            function (i) {
                # Copy the item to the armor stand's mainhand
                if score deref $slot matches $i
                    item replace entity @s weapon.mainhand from entity @a[tag=$self_tag,limit=1] ${'container.' + (i + 9)}
                # Get the item id
                function inventory_tweaks:sort/id
                # If the armor stand doesn't hold anything, kill it
                if score deref $id matches 0 kill @s
                # If the id is not 0, copy it to the armor stand's Pos[1], scale down
                unless score deref $id matches 0
                    set @s.Pos[1] * ${Math.pow(2, -15)} (double) = $id
            }
        )
        function /tree
        set $slot += 1
        
        tellraw @a ${new text_component(pointer<@s.Pos[1]>)}
    }
    # Place items from the armor stands to a shulker box based on their Pos[1]
    set ${shulker_box(0)}.Items = []
    set $slot_id = 0
    
    as @e[type=minecraft:armor_stand,tag=$summon_tag,x=$load_x,y=-2,z=$load_z,sort=nearest] function /../insert {
        function inventory_tweaks:sort/tree
        kill @s
        set $slot_id += 1
    }
    
    # Loot the shulker box to another shulker box to merge items
    set ${shulker_box(1)}.Items = []
    loot insert ${shulker_box(1)} mine ${shulker_box(0)} air{drop_contents:1b}
    
    # Loot the final shulker box to the player
    loot replace entity @s container.9 mine ${shulker_box(1)} air{drop_contents:1b}

}
tag @s remove $self_tag
